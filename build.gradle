plugins {
  id 'application'
  id 'jacoco'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.14.RELEASE'
  id 'org.springframework.boot' version '2.7.12'
  id 'org.owasp.dependencycheck' version '8.1.0'
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'org.sonarqube' version '3.4.0.2513'
  id 'pmd'
}

group = 'uk.gov.hmcts.et'
version = '0.0.1'

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
}

pmd {
  toolVersion = "6.41.0"
  sourceSets = [sourceSets.main, sourceSets.test]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

def sonarExclusions = ['**/uk/gov/hmcts/reform/ethos/ecm/consumer/config/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/model/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/domain/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/tasks/**',
                       '**/uk/gov/hmcts/reform/ethos/ecm/consumer/Application'
]

sonarqube {
  properties {
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.projectKey", "et-message-handler"
    property "sonar.dynamicAnalysis", "reuseReports"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }

  functional {
    java.srcDir 'src/test/functional/java'
    resources.srcDir 'src/test/functional/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
  functionalImplementation.extendsFrom testImplementation
  functionalRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
  exclude '**/*ComponentTest*'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.reform.ethos.ecm.consumer.functional.SmokeTest'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.reform.ethos.ecm.consumer.functional.SmokeTest'
    includeCategories 'uk.gov.hmcts.reform.ethos.ecm.consumer.functional.HealthCheckTest'
  }
  maxHeapSize = '1G'
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
  analyzers.nodeAudit.yarnEnabled = false
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '31.1-jre') {
      entry 'guava'
    }
    dependencySet(group: 'io.netty', version: '4.1.89.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-http'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-transport'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://jitpack.io" }
}

ext {
  feignVersion = '11.8'
  junitPlatform = '1.6.2'
  log4JVersion = '2.20.0'
  logbackVersion = '1.2.11'
  serenity = '3.3.4'
  springBoot = springBoot.class.package.implementationVersion
  tomcatEmbedVersion = '9.0.75'
}

ext["rest-assured.version"] = '5.1.0'

dependencies {
  implementation 'com.github.hmcts:et-common:1.0.5'
  implementation 'com.github.hmcts:et-data-model:1.0.77'
  implementation 'com.github.hmcts:ecm-common:0.1.195'
  implementation 'com.github.hmcts:ecm-data-model:0.0.6'

  implementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.13.3', ext: 'pom'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4.1'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.2'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '5.1.9'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.25.4'
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
  implementation group: 'io.github.openfeign', name: 'feign-core', version: feignVersion
  implementation group: 'junit', name: 'junit', version: '4.13.2'
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.42.0'
  implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.23.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.28'
  implementation group: 'org.apache.santuario', name: 'xmlsec', version: '3.0.2'
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatEmbedVersion
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: tomcatEmbedVersion
  //to remove CVEs
  implementation group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.16', ext: 'pom'
  implementation group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.8'
  implementation group: 'commons-net', name: 'commons-net', version: '3.9.0'
  implementation group: 'com.fasterxml.woodstox', name: 'woodstox-core', version: '6.5.1'
  implementation('org.elasticsearch:elasticsearch') {
    version {
      strictly '7.17.4'
    }
  }

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.4.0'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.1'
  // CVE-2022-25857
  implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.11'
  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'com.microsoft.azure', name: 'adal4j', version: '1.6.7'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-configuration-processor'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.4'
  implementation 'org.springframework.retry:spring-retry'
  implementation group: 'org.testng', name: 'testng', version: '7.7.0'
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.17.3-RELEASE'
  implementation 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation group: 'com.auth0', name: 'java-jwt', version: '4.0.0'
  testImplementation 'com.github.hmcts:fortify-client:1.2.2:all'
  testImplementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: serenity, {
    exclude module: 'jaxb-osgi'
  }
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: serenity
  testImplementation (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
    exclude module: 'slf4j-log4j12'
  }
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

mainClassName = 'uk.gov.hmcts.reform.ethos.ecm.consumer.Application'

bootJar {
  getArchiveFileName().set(provider {
    'et-msg-handler.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
